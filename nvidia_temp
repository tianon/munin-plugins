#!/usr/bin/perl -w
use strict;
use warnings;

#%# family=auto
#%# capabilities=autoconf

use Munin::Plugin;

# TODO use Munin::Plugin::me to support more than just "temp"

my $smi = qx(nvidia-smi -q -x 2>/dev/null);
my @gpus;
while ($smi =~ m! <gpu \s* ([^>]*?) \s* > \s* (.*?) \s* </gpu> !imsxg) {
	my $attr = $1;
	my $xml = $2;
	
	my $gpu = {};
	
	if ($attr =~ m! id=" \s* (\S+) \s* " !ix) {
		$gpu->{id} = $1;
		$gpu->{fieldName} = clean_fieldname $gpu->{id};
	}
	
	if ($xml =~ m! <gpu_temp> \s* (\d+) \s* C \s* </gpu_temp> !ix) {
		$gpu->{tempC} = $1;
	}
	
	if ($xml =~ m! <product_name> \s* ([^<\s][^<]*) \s* </product_name> !ix) {
		$gpu->{name} = $1;
	}
	
	push @gpus, $gpu if defined $gpu->{id} and defined $gpu->{tempC};
}

if (exists $ARGV[0] && $ARGV[0] eq 'autoconf') {
	if (@gpus) {
		print 'yes', "\n";
		exit;
	}
	else {
		print 'no', "\n";
		exit 1;
	}
}

if (exists $ARGV[0] && $ARGV[0] eq 'config') {
	print 'graph_title nVidia Temperature', "\n";
	print 'graph_vlabel Celcius', "\n";
	print 'graph_args --base 1000 -l 0', "\n";
	print 'graph_category sensors', "\n";
	for my $gpu (@gpus) {
		print 'nvidia_gpu_', $gpu->{fieldName}, '.label ', $gpu->{name} || 'Unknown GPU', "\n";
	}
	exit;
}

for my $gpu (@gpus) {
	print 'nvidia_gpu_', $gpu->{fieldName}, '.value ', $gpu->{tempC}, "\n";
}
